// Gradle structure with jar signing capabilities.

// To all contributors
// PLEASE REFRAIN FROM EDITING THIS build.gradle FILE UNLESS ABSOLUTELY NEEDED!!!

buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://dist.creeper.host/Sponge/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
    }
}

ext {
    timeOfExecution = new Date().format( "yyyy-MM-dd'T'HH:mm:ssZ" )
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'signing'

version = '2.0.0-pre1'
group = 'me.jonathing.minecraft'
archivesBaseName = 'foragecraft'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

def canJarBeSigned() {
    return getVariable('fcKeyStore') != null
}

// Returns the public fingerprint, may be empty ("")
def getProjectFingerprint() {
    if (canJarBeSigned()) {
        def sha1 = getVariable('fcSignSHA1')
        if (sha1 == null) return ''
        return sha1.replaceAll(':', '').toLowerCase()
    }
}

String getVariable(String key) {
    return rootProject.hasProperty(key) ? rootProject.property(key) : null
}

// Signs a jar file
def sign(File jar) {
    if (canJarBeSigned()) {
        println "Attempting to sign jar: ${jar}"
        ant.signjar(
                destDir: "${jar.parentFile}",
                jar: "${jar}",
                alias: getVariable('fcKeyStoreAlias'),
                storetype: "jks",
                keystore: getVariable("fcKeyStore"),
                storepass: getVariable('fcKeyStorePass'),
                keypass: getVariable('fcKeyStoreKeyPass'),
                verbose: true,
                preservelastmodified: "true"
        )
        println "Jar signed: ${jar}"
    } else {
        println "No keystore property found, jar will not be signed"
    }
}

static def isOsx() {
    def osProp = System.properties['os.name'].toLowerCase()
    return osProp.contains('darwin') || osProp.contains('osx') || osProp.contains('mac')
}

project.ext.canJarBeSigned = { return canJarBeSigned() }
project.ext.getProjectFingerprint = { return getProjectFingerprint() }
project.ext.sign = { File jar -> return sign(jar) }
project.ext.isOsx = { return isOsx() }

// Print Java version to make sure we know what we are doing.
println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = "tterrag's maven"
        url = "https://maven.tterrag.com/"
    }
    maven {
        name = "Shadew's Maven Repository"
        url = "http://maven.shadew.net/"
    }
    maven {
        name = "ModMaven"
        url = "https://modmaven.k-4u.nl/"
    }
    maven {
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
}

minecraft {
    // The mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   Snapshot are built nightly.
    // stable_#            Stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: 'snapshot', version: '20201003-mixed-1.16.3'
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run/client')
            arg "-mixin.config=foragecraft.mixins.json"

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            // Tell Minecraft if it is running in an IDE
            property 'foragecraft.iside', 'true'

            mods {
                foragecraft {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/server')
            arg "-mixin.config=foragecraft.mixins.json"

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            // Tell Minecraft if it is running in an IDE
            property 'foragecraft.iside', 'true'

            args 'nogui'

            mods {
                foragecraft {
                    source sourceSets.main
                }
            }
        }

        testServer {
            // Get all the shit from the server config
            parent minecraft.runs.server

            // Tell Minecraft that we are only running a test server for the CI
            property 'midnight.istestserver', 'true'
        }

        data {
            workingDirectory project.file('run/data')
            arg "-mixin.config=foragecraft.mixins.json"

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            // Tell Minecraft if it is running in an IDE
            property 'foragecraft.iside', 'true'

            // Tell Minecraft if it is running datagen
            property 'foragecraft.datagen', 'true'

            // Workaround for https://github.com/MinecraftForge/ForgeGradle/issues/690
            // Written by Shadew
            if( project.ext.isOsx() ) {
                jvmArg( "-XstartOnFirstThread" )
            }

            args '--mod', 'examplemod', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                foragecraft {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    // Specify the version of Minecraft to use, If this is any group other then 'net.minecraft' it is assumed
    // that the dep is a ForgeGradle 'patcher' dependency. And it's patches will be applied.
    // The userdev artifact is a special name and will get all sorts of transformations applied to it.
    minecraft 'net.minecraftforge:forge:1.16.3-34.1.25'

    // You may put jars on which you depend on in ./libs or you may define them like so..
    // compile "some.group:artifact:version:classifier"
    // compile "some.group:artifact:version"

    // Real examples
    // compile 'com.mod-buildcraft:buildcraft:6.0.8:dev'  // adds buildcraft to the dev env
    // compile 'com.googlecode.efficient-java-matrix-library:ejml:0.24' // adds ejml to the dev env

    // The 'provided' configuration is for optional dependencies that exist at compile-time but might not at runtime.
    // provided 'com.mod-buildcraft:buildcraft:6.0.8:dev'

    // These dependencies get remapped to your current MCP mappings
    // deobf 'com.mod-buildcraft:buildcraft:6.0.8:dev'

    // For more info...
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html

    compileOnly fg.deobf( "mezz.jei:jei-1.16.3:7.3.2.36:api" )
    runtimeOnly fg.deobf( "mezz.jei:jei-1.16.3:7.3.2.36" )

    compileOnly fg.deobf( "com.github.glitchfiend:biomesoplenty:1.16.3-12.0.0.404:api-obf" )
    compile fg.deobf( "com.github.glitchfiend:biomesoplenty:1.16.3-12.0.0.404:universal-obf" )
}

mixin {
    add sourceSets.main, "foragecraft.refmap.json"
}

build {
    dependsOn 'signJar'
}

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    manifest {
        attributes([
                "Specification-Title": "ForageCraft",
                "Specification-Vendor": "Jonathing",
                "Specification-Version": "${version}", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" :"Jonathing",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs": "foragecraft.mixins.json"
        ])
    }
}

task signJar( dependsOn: 'reobfJar' ) {
    doLast {
        project.ext.sign( jar.archivePath )
    }
}
